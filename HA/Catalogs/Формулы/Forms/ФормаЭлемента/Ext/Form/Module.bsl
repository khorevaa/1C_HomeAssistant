
#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДобавитьПеременную(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗавершитьДобавлениеПеременной", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ФизическиеХарактеристики.Форма.ФормаВыбораГрупп",,
		ЭтаФорма, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры
	
&НаКлиенте
Процедура АрифметическоеДействие(Команда)
	Аргумент = Элементы[Команда.Имя];
	ДобавитьАргументНаФорму(Аргумент.Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура Степень(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗавершитьДобавлениеПеременной", ЭтотОбъект, Элементы.Степень);
	
	ОткрытьФорму("Справочник.ФизическиеХарактеристики.Форма.ФормаВыбораГрупп",,
		ЭтаФорма, УникальныйИдентификатор,,, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура Подсказка(Команда)
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		Объект.Подсказка, ,
		"Формула",
		"Объект");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗавершитьДобавлениеПеременной(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры = Элементы.Степень Тогда
		
		Оповещение = Новый ОписаниеОповещения("ЗавершитьВыборСтепени", ЭтотОбъект, РезультатЗакрытия);
		ПоказатьВводЗначения(Оповещение, 2, "ru = 'Введите степень'; en = 'Input a exponent'", Тип("Число"));
		
	Иначе
		
		ДобавитьАргументНаФорму(РезультатЗакрытия);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьВыборСтепени(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение <> Неопределено Тогда
		ДобавитьАргументНаФорму(СтрШаблон("Pow ( %1, %2 )", ДополнительныеПараметры, Значение));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьАргументНаФорму(Аргумент) 
	
	ТекстФормулы = СокрЛП(Объект.Формула);
	
	Если ПустаяСтрока(ТекстФормулы) Тогда
		ТекстФормулы = Аргумент;
	Иначе
		ТекстФормулы = ТекстФормулы + " " + Аргумент;
	КонецЕсли;
	
	Объект.Формула = СокрЛП(ТекстФормулы);
	
КонецПроцедуры

#КонецОбласти