////////////////////////////////////////////////////////////////////////////////
//РегистрСвеедений.ПринадлежностьФайлов.СписокФайловОбъекта
//  Управление коллекцией медиафайлов, отобранных по владельцу
//  
//Параметры формы:
//  СписокФайлов - Список значений, значениями которого являются структуры описания файлов 
//				(см. ПрикрепленныеФайлыКлиентСервер.НоваяСтруктураПрикрепляемогоФайла())
//  ВладелецФайлов - Ссылка на объект, владеющий файлами
//  
////////////////////////////////////////////////////////////////////////////////

&НаКлиенте
Перем РезультатОбработан; // при заполненной переменной форма закрывается без дополнительных проверок

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработка параметров формы
	Параметры.Свойство("ВладелецФайлов", ВладелецФайлов);
	Если Параметры.СписокФайлов.Количество() > 0 Тогда
		Медиафайлы = Параметры.СписокФайлов;
	ИначеЕсли ЗначениеЗаполнено(ВладелецФайлов) Тогда
		Медиафайлы = ПрикрепленныеФайлыСервер.ПрочитатьСписокФайлов(ВладелецФайлов);
	КонецЕсли;
	
	// Значения по умолчанию
	НомерСтраницы        = 1;
	ЧислоСтрокНаСтранице = 5;
	
	// Дополнительные реквизиты
	ДопРеквизитыФормы = Новый Структура;
	ДопРеквизитыФормы.Вставить("ПустаяКартинка",   ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Пустая16.ПолучитьДвоичныеДанные(), УникальныйИдентификатор));
	ДопРеквизитыФормы.Вставить("Аудиофайл",        ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Аудиофайл.ПолучитьДвоичныеДанные(), УникальныйИдентификатор));
	ДопРеквизитыФормы.Вставить("Добавить",         ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ДобавитьЭлементВЯчейку.ПолучитьДвоичныеДанные(), УникальныйИдентификатор));
	ДопРеквизитыФормы.Вставить("ПрочийФайл",       ПоместитьВоВременноеХранилище(БиблиотекаКартинок.ДокументОбъект.ПолучитьДвоичныеДанные(), УникальныйИдентификатор));
	
	// Настройка элементов формы
	Элементы.СтраницаФайлов.Шапка = Ложь;
	
	// Вывод файлов в таблицу
	ОбновитьСтраницуИзСпискаФайлов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если РезультатОбработан = Неопределено И МодифицированностьМедиа Тогда
		РезультатОбработан = Истина;
		Закрыть(Медиафайлы);
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийСписка

&НаКлиенте
Процедура СтраницаФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаФайла = СтраницаФайлов.НайтиПоИдентификатору(ВыбраннаяСтрока);
	Если СтрокаФайла <> Неопределено И СтрокаФайла.ОписаниеФайла <> Неопределено  Тогда
		ПрикрепленныеФайлыКлиент.ВоспроизвестиМедиа(СтрокаФайла.ОписаниеФайла.НавигационнаяСсылка, СтрокаФайла.ОписаниеФайла.СпособОткрытия, 
				СтрокаФайла.ОписаниеФайла.Расширение, СтрокаФайла.ОписаниеФайла.Наименование);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаФайловПередУдалением(Элемент, Отказ)
	
	СтрокаФайла = Элементы.СтраницаФайлов.ТекущиеДанные;
	Если СтрокаФайла <> Неопределено Тогда
		УдалитьФайлИзСписка(Медиафайлы, СтрокаФайла.ИДЭлементаСписка);
		МодифицированностьМедиа = Истина;
		Если СтраницаФайлов.Количество() = 1 И НомерСтраницы > 1 Тогда
			// Открываем предыдущую страницу
			ПоказатьСтраницуСписка(-1);
		Иначе
			ОбновитьСтраницуИзСпискаФайлов();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ДобавитьФайлКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура СтраницаФайловПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	СтрокаФайла = Элементы.СтраницаФайлов.ТекущиеДанные;
	Если СтрокаФайла <> Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СтруктураФайла", СтрокаФайла.ОписаниеФайла);
		ПараметрыФормы.Вставить("ВладелецФайлов", ВладелецФайлов);
		ПараметрыФормы.Вставить("Представление", СтрокаФайла.Представление);
		
		Оповещение = Новый ОписаниеОповещения("ИзменениеФайлаЗавершение", ЭтотОбъект, Элементы.СтраницаФайлов.ТекущаяСтрока);
		
		ОткрытьФорму("РегистрСведений.ПринадлежностьФайлов.Форма.РедакторЗаписи", ПараметрыФормы, , , , ЭтотОбъект, Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьВсе(Команда)
	
	Если Медиафайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	#Область ПоказатьВопрос
	ТекстВопроса = НСтр("ru='Файлов в списке: %1
		|Удалить все файлы?'");
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, Медиафайлы.Количество());
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("УдалитьВсеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да,     НСтр("ru='Удалить'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, Кнопки, , КодВозвратаДиалога.Отмена, Заголовок);
	#КонецОбласти
	
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	ПоказатьСтраницуСписка(-1);
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	ПоказатьСтраницуСписка(1);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФото(Команда)
	ДобавитьФайлКлиентЗавершение(Новый Структура("Значение", "Фото"), Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьАудио(Команда)
	ДобавитьФайлКлиентЗавершение(Новый Структура("Значение", "Аудио"), Неопределено);
КонецПроцедуры


#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура ОбновитьСтраницуИзСпискаФайлов()

	Если НомерСтраницы = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтраницаФайлов.Очистить();
	
	НачальныйЭлемент = (НомерСтраницы - 1) * ЧислоСтрокНаСтранице;
	КонечныйЭлемент = Мин(Медиафайлы.Количество() - 1, НачальныйЭлемент + ЧислоСтрокНаСтранице - 1);
	
	Для Счетчик = НачальныйЭлемент По КонечныйЭлемент Цикл
		
		ЭлементСписка = Медиафайлы[Счетчик];
		СтрокаТаблицы = СтраницаФайлов.Добавить();
		ЗаполнитьСтрокуСтруктурой(СтрокаТаблицы, ЭлементСписка.Значение, ЭлементСписка.ПолучитьИдентификатор()); 

	КонецЦикла;
	
	ОбновитьНавигациюПоСтраницам(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УдалитьФайлИзСписка(Список, ИДЭлемента)

	ЭлементСписка = Список.НайтиПоИдентификатору(ИДЭлемента);
	Если ЭлементСписка <> Неопределено Тогда
		Список.Удалить(ЭлементСписка);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьФайлВСписке(Список, СтруктураФайла, ИДЭлемента)

	ЭлементСписка = Список.НайтиПоИдентификатору(ИДЭлемента);
	Если ЭлементСписка <> Неопределено Тогда
		ЭлементСписка.Значение      = СтруктураФайла;
		ЭлементСписка.Представление = СтруктураФайла.Наименование;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлКлиент()

	Меню = Новый СписокЗначений;
	Меню.Добавить("Фото",  "Сделать фогографию", , БиблиотекаКартинок.Камера48);
	Меню.Добавить("Аудио", "Записать звук", , БиблиотекаКартинок.Микрофон48);
	
	Оповещение = Новый ОписаниеОповещения("ДобавитьФайлКлиентЗавершение", ЭтотОбъект);
	ПоказатьВыборИзМеню(Оповещение, Меню, Элементы.ДекорацияОтступ);

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайлКлиентЗавершение(Выбор, ДополнительныеПараметры) Экспорт

	Если Выбор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФормаВладельца = ?(ВладелецФормы = Неопределено, ЭтотОбъект, ВладелецФормы);
	
	Если Выбор.Значение = "Фото" Тогда
		Данные = ПрикрепленныеФайлыКлиент.НоваяФотография(,ФормаВладельца);
	Иначе
		Данные = ПрикрепленныеФайлыКлиент.НоваяАудиозапись(ФормаВладельца);
	КонецЕсли;
	
	Если Данные <> Неопределено Тогда
		
		// Добавляем файл в список
		Элемент = Медиафайлы.Добавить(Данные, Данные.Наименование);
		МодифицированностьМедиа = Истина;
		
		// В зависимости от количества уже выведенных файлов либо добавляем файл в таблицу, 
		//либо открываем новую страницу списка
		Если СтраницаФайлов.Количество() = ЧислоСтрокНаСтранице Тогда
			
			// новый файл всегда добавляется в конец списка
			ПоказатьСтраницуСписка(9999);
			
		Иначе
			
			СтрокаТаблицы = СтраницаФайлов.Добавить();
			ЗаполнитьСтрокуСтруктурой(СтрокаТаблицы, Данные, Элемент.ПолучитьИдентификатор());
			ОбновитьНавигациюПоСтраницам(ЭтотОбъект);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИзменениеФайлаЗавершение(Результат, ИДСтроки) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = СтраницаФайлов.НайтиПоИдентификатору(ИДСтроки);
	Если СтрокаТаблицы <> Неопределено Тогда
		ЗаполнитьСтрокуСтруктурой(СтрокаТаблицы, Результат, СтрокаТаблицы.ИДЭлементаСписка);
		ОбновитьФайлВСписке(Медиафайлы, Результат, СтрокаТаблицы.ИДЭлементаСписка);
		МодифицированностьМедиа = Истина;
		ОбновитьНавигациюПоСтраницам(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ЗаполнитьСтрокуСтруктурой(СтрокаТаблицы, Данные, ИД)

	СтрокаТаблицы.ОписаниеФайла = Данные;
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТаблицы.ОписаниеФайла, "Наименование,Комментарий");
	
	СтрокаТаблицы.Представление = ПредставлениеФайлаНаФорме(СтрокаТаблицы.ОписаниеФайла.СпособОткрытия,
										СтрокаТаблицы.ОписаниеФайла.НавигационнаяСсылка, Ложь);
	
	СтрокаТаблицы.ИДЭлементаСписка = ИД;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеФайлаНаФорме(СпособОткрытия, НавигационнаяСсылка, РежимДобавления)

	Возврат ПрикрепленныеФайлыСервер.ПредставлениеФайлаНаФорме(СпособОткрытия,
					НавигационнаяСсылка, РежимДобавления);

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНавигациюПоСтраницам(Форма)

	ВсегоСтраниц = ВсегоКоличествоСтраниц(Форма.Медиафайлы, Форма.ЧислоСтрокНаСтранице);
	Если ВсегоСтраниц = 0 Тогда
		
		Форма.Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Форма.Элементы.СтраницаНетФайлов;
		Форма.Элементы.Назад.Видимость = Ложь;
		Форма.Элементы.Далее.Видимость = Ложь;
		
	Иначе
		
		Форма.Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Форма.Элементы.СтраницаСписок;
		Если ВсегоСтраниц = 1 Тогда
			
			Форма.Элементы.Назад.Видимость = Ложь;
			Форма.Элементы.Далее.Видимость = Ложь;
			
		Иначе
			
			Форма.Элементы.Назад.Видимость = Форма.НомерСтраницы > 1;
			Форма.Элементы.Далее.Видимость = Истина;
			Форма.Элементы.Далее.Доступность = Форма.НомерСтраницы < ВсегоСтраниц;
			
		КонецЕсли;
		
	КонецЕсли;
	 
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВсеЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Медиафайлы.Очистить();
		НомерСтраницы = 1;
		ОбновитьСтраницуИзСпискаФайлов();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСтраницуСписка(Сдвиг)

	ВсегоСтраниц = ВсегоКоличествоСтраниц(Медиафайлы, ЧислоСтрокНаСтранице);
	Если Сдвиг < 0 Тогда
		НомерСтраницы = Макс(1, НомерСтраницы + Сдвиг);
	Иначе
		НомерСтраницы = Мин(ВсегоСтраниц, НомерСтраницы + Сдвиг);
	КонецЕсли;
	
	ОбновитьСтраницуИзСпискаФайлов();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВсегоКоличествоСтраниц(Знач Список, Знач СтрокНаСтранице)

	РазмерСписка = Список.Количество();
	Возврат Цел(РазмерСписка / СтрокНаСтранице) + ?(РазмерСписка % СтрокНаСтранице > 0, 1, 0);

КонецФункции


#КонецОбласти

