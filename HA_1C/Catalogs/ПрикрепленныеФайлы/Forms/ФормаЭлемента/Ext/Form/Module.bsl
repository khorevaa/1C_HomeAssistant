////////////////////////////////////////////////////////////////////////////////
//Справочник.ПрикрепленныеФайлы.ФормаЭлемента
//  
//Параметры формы:
//  Стандартные параметры формы
//  
////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	АктуализироватьНабор();
	
	СсылкаНаДанные = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "ДанныеФайла");
	ОбновитьПредставлениеДанных();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если НаборВладельцев.Количество() = 0 Тогда
		АктуализироватьНабор();
	Иначе
		ЗаписатьНабор();
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры


#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы


#КонецОбласти


#Область ОбработчикиНабораЗаписейРегистраСведений



#КонецОбласти 


#Область ОбработчикиКомандФормы



#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПредставлениеДанных()

	Если Объект.СпособОткрытияФайла = Перечисления.СпособОткрытияПрикрепленногоФайла.КакИзображение
		Или Объект.СпособОткрытияФайла = Перечисления.СпособОткрытияПрикрепленногоФайла.КакВидеоЗапись Тогда
		СсылкаНаПредставлениеДанных = СсылкаНаДанные;
	ИначеЕсли Объект.СпособОткрытияФайла = Перечисления.СпособОткрытияПрикрепленногоФайла.КакАудиоФайл Тогда
		СсылкаНаПредставлениеДанных = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.Аудиофайл.ПолучитьДвоичныеДанные(), УникальныйИдентификатор);
	Иначе
		СсылкаНаПредставлениеДанных = "";
	КонецЕсли;
	
	ИнформацияОФайле = НСтр("ru='Изменен: %1, размер: %2'");
	ИнформацияОФайле = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ИнформацияОФайле, 
					Формат(Объект.ДатаСоздания, "Л=ru_RU; ДЛФ=DT"), Формат(Объект.РазмерФайла/1024, "ЧДЦ=") + " kb.");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Если Форма.Объект.Ссылка.Пустая() Тогда
		Форма.Элементы.ГруппаСтраницыФормы.ТекущаяСтраница = Форма.Элементы.СтраницаОсновное;
		Форма.Элементы.СтраницаВладельцы.Доступность = Ложь;
	Иначе
		Форма.Элементы.СтраницаВладельцы.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура АктуализироватьНабор()

	НаборВладельцев.Очистить();
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей();
	Набор.Отбор.ПрикрепленныйФайл.Установить(Объект.Ссылка);
	Набор.Прочитать();
	
	НаборВладельцев.Загрузить(Набор.Выгрузить());

КонецПроцедуры

&НаСервере
Процедура ЗаписатьНабор()

	Набор = РегистрыСведений.ПринадлежностьФайлов.СоздатьНаборЗаписей();
	Набор.Отбор.ПрикрепленныйФайл.Установить(Объект.Ссылка);
	
	ТаблицаВладельцев = НаборВладельцев.Выгрузить();
	ТаблицаВладельцев.Свернуть("ВладелецФайла");
	
	Для каждого Строка Из ТаблицаВладельцев Цикл
		
		Запись = Набор.Добавить();
		Запись.ВладелецФайла = Строка.ВладелецФайла;
		Запись.ПрикрепленныйФайл = Объект.Ссылка;
		
	КонецЦикла;
	
	Набор.Записать(Истина);

КонецПроцедуры

#КонецОбласти