////////////////////////////////////////////////////////////////////////////////
// ПрикрепленныеФайлыКлиентСервер
//	Функционал подсистемы, общий для клиента и сервера
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает структуру для хранения информации о прикрепленном файле: наименование, двоичные данные, 
//	навигационная ссылка и т.д.
// 
// Возвращаемое значение:
//  Структура - структура для хранения и передачи медиа-данных
//	описание ключей см. в комментариях кода
//
Функция НоваяСтруктураПрикрепляемогоФайла() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("СпособОткрытия", Неопределено); // ПеречислениеСсылка.СпособОткрытияПрикрепленногоФайла
														// Определяет, как система должна обрабатывать двоичные данные
	
	Результат.Вставить("ДатаСоздания", Неопределено); // Дата создания файла. Используется для формирования автонаименования
												// и сравнения версий
	
	Результат.Вставить("Расширение", ""); // Расширение файла, назначенное при его создании
										// Обычно назначается приложением, создающим файл
	
	Результат.Вставить("Наименование", ""); // Наименование файла без расширения
											// формируется автоматически, чтобы при открытии файла во внешних приложениях
											// пользователь мог по имени файла узнать обрабатываемые данные
	
	Результат.Вставить("Комментарий", ""); // произвольный текст, добавляется пользователем по желанию
	
	Результат.Вставить("ПрикрепленныйФайл", Неопределено); // СправочникСсылка.ПрикрепленныеФайлы - запись, в которой хранится файл в информационной базе 
														// Для новых файлов пустая, для считанных из базы - заполнена существующей ссылкой
	
	Результат.Вставить("НавигационнаяСсылка", ""); // Навигационная ссылка. В случае нового файла содержит адрес временного хранилища, 
												//а для файлов, записанных в справочнике - навигационную ссылку 
												//на реквизит справочника ПрикрепленныеФайлы
	
	Возврат Результат;
	
КонецФункции

// Генерирует и возвращает новое наименование для прикрепленного файла
//
//Параметры:
//	СпособОткрытия	- ПеречислениеСсылка.СпособОткрытияПрикрепленногоФайла
//	ДатаФайла	    - Дата - дата создания файла
//
//Возвращаемое значение:
//	Строка - Новое автоматическое наименование
Функция АвтоНаименованиеФайла(СпособОткрытия, ДатаФайла) Экспорт

	Наименование = Формат(?(ЗначениеЗаполнено(ДатаФайла), ДатаФайла, ТекущаяДата()), "Л=ru_RU; ДЛФ=DT");
	Если СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакИзображение") Тогда
		Наименование = НСтр("ru='Фото '") + Наименование;
	ИначеЕсли СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакАудиоФайл") Тогда
		Наименование = НСтр("ru='Аудиозапись '") + Наименование;
	ИначеЕсли СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакВидеоЗапись") Тогда
		Наименование = НСтр("ru='Видео '") + Наименование;
	ИначеЕсли СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакТекст") Тогда
		Наименование = НСтр("ru='Текстовый файл '") + Наименование;
	ИначеЕсли СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакДокументПлатформы") Тогда
		Наименование = НСтр("ru='Таблица '") + Наименование;
	ИначеЕсли СпособОткрытия = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакHTML") Тогда
		Наименование = НСтр("ru='Веб-страница '") + Наименование;
	Иначе
		Наименование = НСтр("ru='Файл '") + Наименование;
	КонецЕсли;

	Возврат Наименование;
	
КонецФункции

// Возвращает наименование без пробельных и спецальных символов 
//
//Параметры:
//	Наименование	- Строка
//
//Возвращаемое значение:
//	Строка
Функция НаименованиеБезСлужебныхСимволов(Знач Наименование) Экспорт

	Наименование = СокрЛП(Наименование);
	
	Наименование = СтрЗаменить(Наименование, "*", " ");
	Наименование = СтрЗаменить(Наименование, "/", " ");
	Наименование = СтрЗаменить(Наименование, "\", " ");
	Наименование = СтрЗаменить(Наименование, ",", " ");
	Наименование = СтрЗаменить(Наименование, "?", " ");
	Наименование = СтрЗаменить(Наименование, "!", " ");
	Наименование = СтрЗаменить(Наименование, ".", "-");
	Наименование = СтрЗаменить(Наименование, ":", "-");
	Наименование = СтрЗаменить(Наименование, "`", " ");
	Наименование = СтрЗаменить(Наименование, "~", " ");
	Наименование = СтрЗаменить(Наименование, "$", " ");
	Наименование = СтрЗаменить(Наименование, "&", " ");
	Наименование = СтрЗаменить(Наименование, "#", " ");
	Наименование = СтрЗаменить(Наименование, "@", " ");
	Наименование = СтрЗаменить(Наименование, """", " ");
	Наименование = СтрЗаменить(Наименование, "|", " ");
	Наименование = СтрЗаменить(Наименование, "  ", " ");
	Наименование = СтрЗаменить(Наименование, "  ", " ");

	Возврат Наименование;
	
КонецФункции

// Возвращает строковое описание списка медиа файлов
//
// Параметры:
//  СписокФайлов - СписокЗначений - список структур данных медиа файлов (см.СтруктураФайлаИзДанныхМультимедиа) 
// 
// Возвращаемое значение:
//  Строка - Строковое представление медиа-файлов
//
Функция ПолучитьОписаниеСпискаМедиаФайлов(СписокФайлов) Экспорт
	
	Если (СписокФайлов = Неопределено) ИЛИ (ТипЗнч(СписокФайлов) <> Тип("СписокЗначений")) Тогда
		Возврат "";
	КонецЕсли;
	
	Если СписокФайлов.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	фото = 0;
	аудио = 0;
	прочее = 0;
	
	КакИзображение = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакИзображение");
	КакАудио = ПредопределенноеЗначение("Перечисление.СпособОткрытияПрикрепленногоФайла.КакАудиоФайл");
	
	Для каждого ЭлементСписка Из СписокФайлов Цикл
		
		Файл = ЭлементСписка.Значение;
		Если Файл.СпособОткрытия = КакАудио Тогда
			аудио = аудио + 1;
		ИначеЕсли Файл.СпособОткрытия = КакИзображение Тогда
			фото = фото + 1;
		Иначе
			прочее = прочее + 1;
		КонецЕсли;
		
	КонецЦикла;
	
	//Формируем результат
	Результат = "";
	Если фото > 0 Тогда
		Результат = Формат(фото, "ЧДЦ=") + " " + НСтр("ru='фото'"); 
	КонецЕсли;
	
	Если аудио > 0 Тогда
		Результат = Результат + ?(Результат = "", "", ", ") + Формат(аудио, "ЧДЦ=") + " " + НСтр("ru='аудио'"); 
	КонецЕсли;
	
	Если прочее > 0 Тогда
		Результат = Результат + ?(Результат = "", "", ", ") + Формат(прочее, "ЧДЦ=") + " " + НСтр("ru='др. файлов'");
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции

// Обновляет надписи и доступность команд управления прикрепленными файлами на форме
Процедура ОбновитьПредставлениеМедиафайлов(Форма) Экспорт

	КоличествоФайлов = Форма.МедиаФайлы.Количество();
	Форма.Элементы.ОбщаяКомандаСписокФайлов.Заголовок = "Файлы: " + КоличествоФайлов; 
	
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции



#КонецОбласти