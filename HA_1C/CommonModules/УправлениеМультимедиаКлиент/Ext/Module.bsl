//////////////////////////////////////////////////////////////////////////////////////////
// Интерфейс управления средствами мультимедиа на мобильном устройстве
// 
//////////////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Снимает фотографию, при помощи камеры устройства
//
// Параметры:
//  ТипКамеры	 -  ТипКамерыУстройства - Какой камерой будет сделан снимок, если не задан, то по-умолчанию режим Авто.
//  Качество	 - Число - Качество снимка в процентах
//  ЧерноБелый	 - Булево - Истина, если требуется сделать ч/б фото
// 
// Возвращаемое значение:
//  * ДанныеМультимедиа - Данные фотографии
//  * Неопределено - Снимок не удалось сделать или фотография не поддерживается самим устройством
//
Функция СделатьФотографию(ТипКамеры = Неопределено, Качество = 100, ЧерноБелый = Ложь) Экспорт	
	
	#Если МобильноеПриложениеКлиент Тогда
		ТекТипКамеры = ТипКамерыУстройства.Авто;
		Если ТипКамеры <> Неопределено Тогда
			ТекТипКамеры = ТипКамеры;
		КонецЕсли;
		
		Если СредстваМультимедиа.ПоддерживаетсяФотоснимок(ТекТипКамеры) Тогда
			Если Не УправлениеМультимедиаВызовСервера.ДелатьФотоАндроидАПИ() Тогда
				Возврат СделатьФотоСредствами1С(ТекТипКамеры);
			Иначе
				Возврат СделатьФотоСредствамиAndroid();
			КонецЕсли;
		Иначе
			 ВызватьИсключение НСтр("ru='Выбранная камера не поддерживается Вашим устройством!'");
		КонецЕсли;
	#Иначе
		Возврат Неопределено;
	#КонецЕсли
	
КонецФункции

// Делает аудиозапись средствами мобильного устройства
// 
// Возвращаемое значение:
//  * ДанныеМультимедиа - Данные аудиозаписи
//  * Неопределено - Не удалось сделать запись или микрофон не поддерживается самим устройством
//
Функция СделатьАудиозапись() Экспорт	
	
	#Если МобильноеПриложениеКлиент Тогда		
		Если СредстваМультимедиа.ПоддерживаетсяАудиозапись() Тогда
			Возврат СредстваМультимедиа.СделатьАудиозапись();
		Иначе			
			ВызватьИсключение НСтр("ru='Ваше устройство не поддерживает аудиозапись!'");
		КонецЕсли;
	#Иначе
		Возврат Неопределено;	
	#КонецЕсли
	
КонецФункции

// Выполняет видеозапись с выбранной камеры устройства
//
// Параметры:
//  Качество	 - КачествоВидеозаписи - Качество записи. Если не указан, то Авто
//  ТипКамеры	 -  ТипКамерыУстройства - Какой камерой будет сделана запись, если не задан, то по-умолчанию режим Авто.
// 
// Возвращаемое значение:
//  * ДанныеМультимедиа - Данные видеозаписи
//  * Неопределено - Не удалось сделать запись или камера не поддерживается самим устройством
//
Функция СделатьВидеозапись(Качество = Неопределено, ТипКамеры = Неопределено) Экспорт	
	
	#Если МобильноеПриложениеКлиент Тогда
		ТекКачество = КачествоВидеозаписи.Авто;
		Если Качество <> Неопределено Тогда
			ТекКачество = Качество;
		КонецЕсли;	
		
		ТекТипКамеры = ТипКамерыУстройства.Авто;
		Если ТипКамеры <> Неопределено Тогда
			ТекТипКамеры = ТипКамеры;
		КонецЕсли;	
		
		Если СредстваМультимедиа.ПоддерживаетсяВидеозапись(ТекТипКамеры) Тогда
			Возврат СредстваМультимедиа.СделатьВидеозапись(ТекТипКамеры, ТекКачество);
		Иначе			
			 ВызватьИсключение НСтр("ru='Выбранная камера не поддерживается Вашим устройством!'");
		КонецЕсли;
	#Иначе
		Возврат Неопределено;	
	#КонецЕсли
	
КонецФункции

// Открывает картинку из библиотеки устройства и возвращает выбранный файл
// 
// Возвращаемое значение:
//  Структура - Данные картинки
//
Функция ОткрытьКартинкуИзПамяти() Экспорт
	
	Результат = Неопределено;
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Каталог = ДанныеИБ.КаталогФото();
	
	Если Диалог.Выбрать() Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Файл", Неопределено);
		Результат.Вставить("РасширениеФайла", "");
		
		ВыбраннаяКартинка = Новый Картинка(Диалог.ПолноеИмяФайла);
		Результат.Файл = ВыбраннаяКартинка;
		ФайлКартинки = Новый Файл(Диалог.ПолноеИмяФайла);
		Результат.РасширениеФайла = ФайлКартинки.Расширение;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Вызывает внешнее приложение для редактирования картинки и возвращает результат
// Приложение должно быть установлено на телефоне. См. макет HA_Paint в обработке НастройкиПрограммы
//
// Параметры:
//  Адрес  - Строка - ссылка на временное хранилище и навигационная ссылка, 
//                    по которому находятся картинка для редактирования.
// Результат:
//  Структура - данные картинки
//
Функция ИзменитьФотоВPaint(Адрес) Экспорт
	
	Результат = Неопределено;
	
	#Если МобильноеПриложениеКлиент Тогда
	
	Картинка = УправлениеМультимедиаВызовСервера.ПрочитатьКартинкуИзБазы(Адрес);
	ПутьКартинки = ДанныеИБ.КаталогКартинок() + Новый УникальныйИдентификатор() + ".png";
	Картинка.Записать(ПутьКартинки);
	
	Приложение = Новый ЗапускПриложенияМобильногоУстройства();
	Приложение.Действие = "com.ripreal.homeassistant.PaintActivity";
	Приложение.ДополнительныеДанные.Добавить("image", ПутьКартинки, "String");
	Приложение.Запустить(Истина);
	
	ПутьОбработаннойКартинки = Приложение.ДополнительныеДанные.Получить("image").Значение;
	
	ФайлОбработаннойКартинки = Новый Файл(ПутьОбработаннойКартинки);
	
	Если ФайлОбработаннойКартинки.Существует() Тогда
		
		Результат = Новый Структура;
		ОбработаннаяКартинка = Новый Картинка(ПутьОбработаннойКартинки);
		
		Результат.Вставить("Файл", ОбработаннаяКартинка);
		Результат.Вставить("РасширениеФайла", ФайлОбработаннойКартинки.Расширение);
		
	КонецЕсли;
	
	УдалитьФайлы(ПутьКартинки);
	УдалитьФайлы(ПутьОбработаннойКартинки);
	
	#КонецЕсли
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СделатьФотоСредствами1С(ТекТипКамеры)
	#Если МобильноеПриложениеКлиент Тогда
		Возврат СредстваМультимедиа.СделатьФотоснимок(ТекТипКамеры);
	#КонецЕсли
КонецФункции

Функция СделатьФотоСредствамиAndroid()
		
	#Если МобильноеПриложениеКлиент Тогда
		
		ФайлКартинки = "file:///sdcard/DCIM/Camera/" + Новый УникальныйИдентификатор() + ".png";
		НовВз = Новый ЗапускПриложенияМобильногоУстройства("android.media.action.IMAGE_CAPTURE");
		
		//Обязательно указываем этот параметр, если его не указать, тогда вам вернется привью файла в низком качестве и находится он будет в параметрах с ключем data.
		НовВз.ДополнительныеДанные.Добавить("output", ФайлКартинки, "Uri");
		
		//Если фото не сделано, то ответ будет "0"
		Ответ = НовВз.Запустить(Истина);
		
		Если Ответ <> 0 Тогда
			Картинка = Новый Картинка(ФайлКартинки);
			// need to delete file created by android and put it in the temp storage
			//Адрес = ПоместитьВоВременноеХранилище(Картинка);
			УдалитьФайлы(ФайлКартинки);
			Возврат Картинка;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
		
	#КонецЕсли
	
КонецФункции

#КонецОбласти